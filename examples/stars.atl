; Horizontal Stars
; Demo program for ATALAN programming language
;(c) 2010 Rudla Kudla

i:0..242				; number of lines with stars
hpos:array(242) of byte
speed:array(242) of 1..4
col:array(242) of color

;Turn off screen, so we have nice black deep space

DMACTL = 0
GRACTL = 0

;Initialize start positions, speeds and colors

for i 
	hpos(i)  = RANDOM
	speed(i) = (RANDOM mod 4) + 1
	col(i)   = RANDOM bitor 3			;make the star color light enough 

;Setup sprite 0

player_gfx(0) = 1
player_size(0) = 0

until STRIG(0) = pressed

	until VCOUNT = 4 
		wait'line
		
	for i
		player_col2(0) = col(i)
		player_x(0) = hpos(i)
		hpos(i) = hpos(i) + speed(i)
		wait'line

;Turn off sprite and turn on screen

player_gfx(0) = 0
player_gfx(1) = 0
GRACTL = 0
DMACTL = 34
