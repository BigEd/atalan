;*****************************
; Type inferencing
;*****************************

;Atalan can deduce automatically the type of variable, if it has not been defined by user.

min:0..1000
max:0..1000

;All integer variables have built-in associated constants min and max defining
;minimal and maximal possible value of the variable. 

x0:13..100
min = x0.min
max = x0.max
"x0:[min]..[max]"

;>> x0:13..100

;Assigning constant n to variable, initializes the type as n..n

x1 = 15
min = x1.min
max = x1.max
"x1:[min]..[max]"

;>> x1:15..15

;Assigning a variable to variable copies the type of source variable.

x2 = x0
min = x2.min
max = x2.max
"x2:[min]..[max]"
;>> x2:13..100

;Operators are correctly handled. In this case, RANDOM is variable of type 0..255
;And changes the type to 0..63 and multiplication by two expands it to 0..126.
;Finally adding 1 shifts it to 1..127.

x3 = (RANDOM and 63) * 2 + 1
min = x3.min
max = x3.max
"x3:[min]..[max]"

;>> x3:1..127

;Multiple assignments to variable are merged together as union.

x4 = x0
x4 = 107
x4 = 10
min = x4.min
max = x4.max
"x4:[min]..[max]"

;>> x4:10..107

;In case variable references itself, type can not be inferenced and must be
;declared explicitly by programmer.

x5 = 100
x5 = x5 + 1

min = x5.min
max = x5.max
"x5:[min]..[max]"
